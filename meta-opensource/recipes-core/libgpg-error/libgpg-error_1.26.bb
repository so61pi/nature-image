#======================================#
#           SUMMARY/LICENCE            #
#--------------------------------------#
SUMMARY = "Small library that defines common error values for all GnuPG components"
HOMEPAGE = "http://www.gnupg.org/related_software/libgpg-error/"

LICENSE = "GPLv2+ & LGPLv2.1+"
LIC_FILES_CHKSUM = "file://COPYING;md5=59530bdf33659b29e73d4adb9f9f6552                         \
                    file://COPYING.LIB;md5=2d5025d4aa3495befef8f17206a5b0a1                     \
                    file://src/gpg-error.h.in;endline=23;md5=64af9846baaf852793fd3a5af393acbf   \
                    file://src/init.c;endline=20;md5=872b2389fe9bae7ffb80d2b91225afbc           \
                    "


#======================================#
#           REQUIRE/INHERIT            #
#--------------------------------------#
BINCONFIG = "${bindir}/gpg-error-config"

inherit autotools binconfig-disabled pkgconfig


#======================================#
#                SOURCE                #
#--------------------------------------#
SRC_URI = "${GNUPG_MIRROR}/libgpg-error/libgpg-error-${PV}.tar.bz2;name=source  \
           file://pkgconfig.patch                                               \
           "

SRC_URI[source.md5sum] = "97456709dbbcbb69354317ffe3e347cd"
SRC_URI[source.sha256sum] = "4c4bcbc90116932e3acd37b37812d8653b1b189c1904985898e860af818aee69"


#======================================#
#           DEPENDS/PROVIDES           #
#--------------------------------------#
DEPENDS        = " gettext-native "
DEPENDS_append = " virtual/gettext "


#======================================#
#         BUILD CONFIGURATIONS         #
#--------------------------------------#
CPPFLAGS_append = " -P "


#======================================#
#                TASKS                 #
#--------------------------------------#
do_compile_prepend() {
    TARGET_FILE=linux-gnu
    if [ ${TARGET_OS} = "mingw32" ]; then
        # There are no arch specific syscfg files for mingw32
        TARGET_FILE=
    elif [ ${TARGET_OS} != "linux" ]; then
        TARGET_FILE=${TARGET_OS}
    fi

    case ${TARGET_ARCH} in
      aarch64_be) TUPLE=aarch64-unknown-linux-gnu ;;
      arm)        TUPLE=arm-unknown-linux-gnueabi ;;
      armeb)      TUPLE=arm-unknown-linux-gnueabi ;;
      i586|i686)  TUPLE=i686-pc-linux-gnu ;;
      mips64*)    TUPLE=mips64el-unknown-linux-gnuabi64 ;;
      mips*el)    TUPLE=mipsel-unknown-linux-gnu ;;
      mips*)      TUPLE=mips-unknown-linux-gnu ;;
      x86_64)     TUPLE=x86_64-pc-linux-gnu ;;
      *)          TUPLE=${TARGET_ARCH}-unknown-linux-gnu ;; 
    esac

    if [ -n "$TARGET_FILE" ]; then
        cp ${S}/src/syscfg/lock-obj-pub.$TUPLE.h \
            ${S}/src/syscfg/lock-obj-pub.$TARGET_FILE.h
    fi
}


do_install_append() {
    # we don't have common lisp in OE
    rm -rf "${D}${datadir}/common-lisp/"
}


#======================================#
#            PACKAGES/FILES            #
#--------------------------------------#
FILES_${PN}-dev_append = " ${bindir}/gpg-error "
FILES_${PN}-doc_append = " ${datadir}/libgpg-error/errorref.txt "


#======================================#
#            INSTALL SCRIPT            #
#--------------------------------------#


#======================================#
#             ALTERNATIVES             #
#--------------------------------------#


#======================================#
#           USERADD/SYSTEMD            #
#--------------------------------------#


#======================================#
#                 TEST                 #
#--------------------------------------#
